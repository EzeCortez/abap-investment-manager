managed implementation in class zbp_i_header_inve unique;
strict ( 2 );
with draft;

define behavior for ZI_HEADER_INVE alias Header
persistent table zheader_inve
draft table zheader_inve_d
lock master
total etag CreatedAt
authorization master ( instance )
{
  create ( authorization : global );
  update;
  delete;
  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare;
  determination CalculateFinancials on save { field NominalAmount, TotalBuyPrice; }
  association _Schedule { create; with draft; }

  field ( numbering : managed, readonly ) InvestId;
  field ( readonly ) CreatedBy, CreatedAt, Status;

  mapping for zheader_inve
  {
    InvestId = invest_id;
    AssetType = asset_type;
    Description = description;
    NominalAmount = nominal_amount;
    CurrencyCode = currency_code;
    TotalBuyPrice = total_buy_price;
    LocalTaxBasis = local_tax_basis;
    Status = status;
    CreatedBy = created_by;
    CreatedAt = created_at;
  }
}

define behavior for ZI_BONO_SCHEDULE alias Schedule
persistent table zbono_schedule
draft table zbono_sched_d
lock dependent by _Header
authorization dependent by _Header
{
  update;
  delete;

  field ( readonly ) InvestId, ScheduleItem;

  association _Header { with draft; }

  mapping for zbono_schedule
  {
    InvestId = invest_id;
    ScheduleItem = schedule_item;
    PaymentDate = payment_date;
    CurrencyCode = currency_code;
    NominalAmort = nominal_amort;
    InterestAmount = interest_amount;
    AppliedRate = applied_rate;
    CerAdjustment = cer_adjustment;
    PaymentReceived = payment_received;
  }
}